#lang rhombus/static/and_meta
import:
  meta:
    "type.rhm" as t
  "type_statinfo.rhm" as t_s
  "value.rhm".value

export:
  rename literal as #%literal

meta:
  fun type_of_literal(lit :: Syntax):
    match lit.unwrap()
    | (_ :: Boolean): t.Bool(lit)
    | (_ :: String): t.Str(lit)
    | (_ :: Number): t.Num(lit)
    | (_ :: Char): t.Chr(lit)
    | (_ :: Keyword):
        syntax_meta.error("keyword is not allowed as an expression", lit)  
    | ~else:
        syntax_meta.error("literal type not known", lit)  

expr.macro 'literal $(lit :: Term) $tail ...':
  values(t_s.wrap_type(Syntax.relocate_span('#%literal $lit', [lit]), type_of_literal(lit)),
         '$tail ...')

value.macro
| 'literal $(lit :: Term)':
    t_s.type_to_syntax(type_of_literal(lit))
| 'literal $_ ...':
    // non-empty tail means more complex expression, assume non value
    '#false'
