#lang rhombus/static/and_meta
import:
  rhombus_dyn open
  meta -1:
    rhombus/meta.syntax_meta

export:
  IdMap

class IdMap(table :~ Dyn):
  internal _IdMap
  private implements:
    Indexable
    Appendable

  fun build([[key :~ Dyn, val], ...]):
    for values (table :~ Map = {}):
      each:
        key: [key, ...]
        val: [val, ...]
      table ++ { key.unwrap(): [Pair(key, val), & table.get(key.unwrap(), [])]}
  
  expression 'IdMap{ $key ...: $val, ...}':
    '_IdMap(build([[$key ..., $val], ...])) :~ IdMap'

  private override get(index :: Identifier):
    let candidates :~ Dyn = table.get(index.unwrap(), [])
    for any:
      each Pair(key, val): candidates
      syntax_meta.equal_binding(key, index) && val

  private override append(other :: IdMap) :~ IdMap:
    _IdMap(for values(table :~ Map = table):
             each (sym, candidates :~ List): other.table
             table ++ { sym: candidates ++ table.get(sym, []) })

  method flatten() :: Dyn:
    for Map:
      each (k, ps :~ List): table
      each Pair(key, val): ps
      values(key, val)

  method val_set() :: Dyn:
    for Set:
      each (k, ps :~ List): table
      each Pair(_, val): ps
      val
